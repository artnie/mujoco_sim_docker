{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "8d73a913-2cee-488e-8279-b846af936ab5",
   "metadata": {},
   "source": [
    "# Demo for the Lab-Opening Event\n",
    "\n",
    "This demo shows an infinite loop of the PR2, picking and placing a bottle onto a bowl. Press the `>>` button to run all cells in sequence."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "610a5372-ac65-4855-8e07-e7aabf687acf",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "(asdf:load-system :cram-pr2-pick-place-demo)\n",
    "(roslisp-utilities:startup-ros)\n",
    "(defpackage cram-bullet-world-tutorial\n",
    "  (:nicknames :btw-tut)\n",
    "  (:use :common-lisp :cram-prolog :desig :exe))\n",
    "(in-package :btw-tut)\n",
    "\n",
    "(defun make-pose (frame point euler)\n",
    "    (declare (type string frame)\n",
    "             (type list point euler))\n",
    "    \"Creates a cl-transforms pose in 3D space w.r.t. the given frame, point and euler.\"\n",
    "    (if (and (eq (length point) 3) (eq (length euler) 3))\n",
    "        (destructuring-bind (ax ay az) (mapcar #'eval euler)\n",
    "          (cl-transforms-stamped:make-pose-stamped frame 0.0 \n",
    "                                                   (apply #'cl-transforms:make-3d-vector point)\n",
    "                                                   (cl-transforms:euler->quaternion :ax ax :ay ay :az az)))\n",
    "        (format T \"point ~a or euler ~a is not of length 3\" point euler)))\n",
    "\n",
    "(defun simulate (&key (duration 5.0)    ;; in seconds \n",
    "                      (framerate 20.0)) ;; in Hz\n",
    "    (let ((sleep-interval (/ 1.0 framerate))\n",
    "          (times (floor (* duration framerate))))\n",
    "         (dotimes (_ times)\n",
    "             (sleep (/ sleep-interval 2))\n",
    "             (btr:simulate btr:*current-bullet-world* (/ sleep-interval 2)))))\n",
    "\n",
    "(defparameter *bottle-spawn-pose* (make-pose \"map\" '(-2.323 -1 0.82) '(0 0 0)))\n",
    "(defparameter *base-pose-near-table* (make-pose \"map\" '(-2.2 -0.2 0.0) '(0.0 0.0 (* pi -0.5))))\n",
    "(defparameter *downward-look-coordinate* (make-pose \"base_footprint\" '(0.7 0.0 1.0) '(0 0 0)))\n",
    "(defparameter *base-pose-near-kitchen-island* (make-pose \"map\" '(-0.15 2.0 0.0) '(0.0 0.0 pi)))\n",
    "(defparameter *object-delivery-pose* (make-pose \"map\" '(-0.8 2 0.9) '(0 0 0)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ec4fed6c-2942-48ee-bcdc-b05d2e47a23c",
   "metadata": {},
   "outputs": [],
   "source": [
    "(defun pick (?object ?nav-goal ?poi &key ?arm)\n",
    "    (let ((?arm (if (or (listp ?arm) (not ?arm))\n",
    "                    ?arm\n",
    "                    `(,?arm)))\n",
    "          ?perceived-object)\n",
    "       (cpl:par\n",
    "             ;; Park arms\n",
    "             (perform (an action\n",
    "                          (type parking-arms)))\n",
    "             ;; Move the torso up\n",
    "             (perform (a motion \n",
    "                         (type moving-torso)\n",
    "                         (joint-angle 0.3)))\n",
    "             ;; Go to the navigation goal\n",
    "             (perform (an action\n",
    "                          (type going)\n",
    "                          (target (a location \n",
    "                                     (pose ?nav-goal))))))\n",
    "         ;; Look at the point of interest\n",
    "         (perform (an action\n",
    "                      (type looking)\n",
    "                      (target (a location \n",
    "                                 (pose ?poi)))))\n",
    "         ;; Detect the object\n",
    "         (setf ?perceived-object \n",
    "               (perform (an action\n",
    "                            (type detecting)\n",
    "                            (object ?object))))\n",
    "         ;; Pick up the detected object\n",
    "         (perform (an action\n",
    "                      (type picking-up)\n",
    "                      (desig:when ?arm\n",
    "                          (arm ?arm))\n",
    "                      (object ?perceived-object)))\n",
    "         ;; Park arms\n",
    "         (perform (an action\n",
    "                      (type parking-arms)))\n",
    "         ?perceived-object))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "19030a7e-e0a2-431b-b50f-eb48210a2398",
   "metadata": {},
   "outputs": [],
   "source": [
    "(defun place (?nav-goal ?drop-pose &key ?object ?arm)\n",
    "    (let ((?arm (if (or (listp ?arm) \n",
    "                        (not ?arm))\n",
    "                    ?arm\n",
    "                    `(,?arm))))\n",
    "         ;; Move the torso up so the robot can reach the higher surface better\n",
    "         ;; without crashing his arms into it.\n",
    "         (perform (a motion \n",
    "                     (type moving-torso)\n",
    "                     (joint-angle 0.3)))\n",
    "         ;; Go to the target delivery location\n",
    "         (perform (an action\n",
    "                      (type going)\n",
    "                      (target (a location\n",
    "                                 (pose ?nav-goal)))))\n",
    "         ;; Place down the bottle\n",
    "         (perform (an action\n",
    "                      (type placing)\n",
    "                      (desig:when ?arm\n",
    "                          (arm ?arm))\n",
    "                      (desig:when ?object\n",
    "                          (object ?object))\n",
    "                      (target (a location\n",
    "                                 (pose ?drop-pose)))))\n",
    "         ;; Park arms\n",
    "         (perform (an action\n",
    "                      (type parking-arms)))))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e5549c28-bac3-4de7-acb6-3c1108f3942f",
   "metadata": {},
   "source": [
    "The following overwrites an event handler, which simulates the world as soon as the robot releases an object from its gripper. This code changes the simulation time to only 0.1 sec, such that you can explicitly simulate after releasing an object in mid air."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "43c24955-3202-4774-bf42-6a3d47b9f9fe",
   "metadata": {},
   "outputs": [],
   "source": [
    "(in-package :cram-bullet-reasoning-belief-state)\n",
    "\n",
    "(defmethod cram-occasions-events:on-event btr-detach-object 2 ((event cpoe:object-detached-robot))\n",
    "  (let* ((robot-object (btr:get-robot-object))\n",
    "         (environment-object (btr:get-environment-object))\n",
    "         (btr-object-name (cpoe:event-object-name event))\n",
    "         (first-arm (first (cpoe:event-arm event)))\n",
    "         (second-arm (second (cpoe:event-arm event)))\n",
    "         (first-link (when first-arm\n",
    "                       (if (get-ee-link first-arm)\n",
    "                           (get-ee-link first-arm)\n",
    "                           (if (cpoe:event-link event)\n",
    "                               (cpoe:event-link event)\n",
    "                               (error \"[BTR-BELIEF OBJECT-DETACHED] either link or arm ~\n",
    "                                 in object-attached-robot even had to be given...\")))))\n",
    "         (second-link (when second-arm\n",
    "                        (if (get-ee-link second-arm)\n",
    "                            (get-ee-link second-arm)\n",
    "                            (if (cpoe:event-link event)\n",
    "                                (cpoe:event-link event)\n",
    "                                (error \"[BTR-BELIEF OBJECT-DETACHED] either link or arm ~\n",
    "                                  in object-attached-robot even had to be given...\"))))))\n",
    "    (when (and (cut:is-var first-link)\n",
    "               (cut:is-var second-link))\n",
    "      (error \"[BTR-BELIEF OBJECT-DETACHED] Couldn't find robot's EE link.\"))\n",
    "    (if btr-object-name\n",
    "        ;; if btr-object-name was given, detach it from the robot link\n",
    "        (let ((btr-object (btr:object btr:*current-bullet-world* btr-object-name)))\n",
    "          (when btr-object\n",
    "            (btr:detach-object robot-object btr-object :link first-link)\n",
    "            (btr:detach-object robot-object btr-object :link second-link)\n",
    "            (btr:simulate btr:*current-bullet-world* 0.1)\n",
    "            ;; find the links and items that support the object\n",
    "            ;; and attach the object to them.\n",
    "            ;; links get proper attachments and items loose attachments\n",
    "            (let ((contacting-links\n",
    "                    (remove-duplicates\n",
    "                     (mapcar\n",
    "                      #'cdr\n",
    "                      (remove-if-not\n",
    "                       ;; filter all the links contacting items to our specific item\n",
    "                       (lambda (item-and-link-name-cons)\n",
    "                         (equal\n",
    "                          (btr:name (car item-and-link-name-cons))\n",
    "                          btr-object-name))\n",
    "                       ;; get all links contacting items in the environment\n",
    "                       (btr:link-contacts environment-object)))\n",
    "                     :test #'equal))\n",
    "                  ;; (contacting-items\n",
    "                  ;;   (remove-if-not\n",
    "                  ;;    (lambda (c) (typep c 'btr:item))\n",
    "                  ;;    (btr:find-objects-in-contact btr:*current-bullet-world* btr-object)))\n",
    "                  )\n",
    "              ;; If btr-object is in contact with an item,\n",
    "              ;; it will be attached loose.\n",
    "              ;; Otherwise, if a link contacting btr-object was found,\n",
    "              ;; btr-object will be attached to it.\n",
    "              (or ;;-- (mapcar (lambda (item-object)\n",
    "                  ;;           (when item-object\n",
    "                  ;;             (btr:attach-object item-object btr-object :loose T)))\n",
    "                  ;;         contacting-items)\n",
    "                  (mapcar (lambda (link-name)\n",
    "                            (btr:attach-object\n",
    "                             environment-object btr-object :link link-name))\n",
    "                          contacting-links)\n",
    "                  (roslisp:ros-warn (btr-belief btr-detach-object)\n",
    "                                    \"Object ~a was detached from robot,\n",
    "                                     but after falling down it\n",
    "                                     is in no contact with the\n",
    "                                     environment or another object.\"\n",
    "                                    btr-object-name)))))\n",
    "        ;; if btr-object-name was not given, detach all objects from the robot link\n",
    "        (progn\n",
    "          (btr:detach-all-from-link robot-object first-link)\n",
    "          (btr:detach-all-from-link robot-object second-link)\n",
    "          (btr:simulate btr:*current-bullet-world* 0.1)))))\n",
    "\n",
    "\n",
    "(in-package :btw-tut)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71727bb7-20d0-4225-b50f-24a7533d99ae",
   "metadata": {},
   "outputs": [],
   "source": [
    ";; original\n",
    ";; (defparameter *object-delivery-pose* (make-pose \"map\" '(-0.8 2 0.9) '(0 0 0)))\n",
    "\n",
    ";; higher up\n",
    "(defparameter *object-delivery-pose* (make-pose \"map\" '(-0.9 2 1.1) '(0 0 0)))\n",
    "\n",
    ";; sideways\n",
    ";; (defparameter *object-delivery-pose* (make-pose \"map\" '(-0.8 2 1.2) '((* 1.5 pi) (* 0.5 pi) pi)))\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cc357ec5-c119-4f47-940b-048620815e88",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    ";; Reset the world\n",
    "(demo::initialize)\n",
    "\n",
    ";; Loop infinitely\n",
    "(loop while t\n",
    "      do\n",
    "      ;; Clear all objects\n",
    "      (btr-utils:kill-all-objects)\n",
    "\n",
    "      ;; Spawn the bottle\n",
    "      (btr-utils:spawn-object 'bottle-1 :bottle :pose *bottle-spawn-pose*)\n",
    "\n",
    "      ;; Spawn the bottle with random offset\n",
    "      (let ((random-bowl-position `(,(+ (random 0.1) -0.9) ,(+ (random 0.1) 1.95) 0.91)))\n",
    "        (btr-utils:spawn-object 'bowl-1 :bowl\n",
    "                                :pose (make-pose \"map\" random-bowl-position '(0 0 0))))\n",
    "\n",
    "      ;; Move the robot\n",
    "      (urdf-proj:with-simulated-robot\n",
    "        (let (?perceived-object)\n",
    "             ;; Pick up the bottle\n",
    "             (pick (an object (type bottle))\n",
    "                *base-pose-near-table*\n",
    "                *downward-look-coordinate*\n",
    "                :?arm :right)\n",
    "   \n",
    "             ;; Place the bottle\n",
    "             (place *base-pose-near-kitchen-island*\n",
    "                    *object-delivery-pose*)))\n",
    "      (simulate))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3d20d8af-8a54-4e8d-9b60-d2aeb389ab35",
   "metadata": {},
   "source": [
    "## Task Tree Export\n",
    "\n",
    "Each action of the robot builds up the task tree of the robot's execution."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bb72a927-9417-4cfa-8f56-60238765def2",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "(ros-load:load-system :cram-task-tree-export)\n",
    "(setf tt-export:*export-file-path* \"/home/lectures/lab_opening_presentation/task_tree_export.dot\")\n",
    "(tt-export:export-task-tree)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7f883d66-fc2c-45d4-b35f-d41fd95232f3",
   "metadata": {},
   "source": [
    "Download the `task_tree_export.dot` file and visualize it with `xdot` on your local machine."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Common Lisp",
   "language": "common-lisp",
   "name": "common-lisp"
  },
  "language_info": {
   "codemirror_mode": "text/x-common-lisp",
   "file_extension": ".lisp",
   "mimetype": "text/x-common-lisp",
   "name": "common-lisp",
   "pygments_lexer": "common-lisp",
   "version": "2.0.1.debian"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
