{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "0f5638c5-8302-4caf-9dbf-01ade549ad2e",
   "metadata": {},
   "source": [
    "# Assignment 1 - Orc Battle (3p)\n",
    "\n",
    "Welcome to the Assignment 1 of the \"Robot Programming with Lisp\" course! \n",
    "\n",
    "This is a text-based RPG game. However, the code is broken. Your task is to fix the code and get the game to run."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9c4cbcd0-dd7a-409f-a303-1127e745daf2",
   "metadata": {},
   "source": [
    "## Task 1. Comment out the assignment text\n",
    "\n",
    "Natural language text is not allowed in Lisp files. The sign for comments is `;` in Lisp. Mark multiple lines with your mouse, or use Shift+Up/Down. Then hit Ctrl+/ to comment all lines at once\n",
    "\n",
    "## Task 2. Open up the 'Show Contextual Help'\n",
    "\n",
    "Right-click somewhere and open up the window for contextual help. This is a helpful window that always shows you the definition of compiled functions, parameters, macros and whatnot. Just move the cursor to the desired symbol. In Lisp, symbols are the names for functions, parameters, variables, etc.\n",
    "\n",
    "## Task 3. Adjust the parameters\n",
    "Below you see a Lisp `defparameter` clause.\n",
    "It is correct only for two variables: `*monster-num*` and `*monsters*`.\n",
    "Turn the line from `*monster-builders*` into `(defparameter *monster-builders* nil)`. Do the same for the rest of the parameters.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c08034bf-6687-440d-a48c-871871a41f30",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    ";; Correct these parameters, then compile.\n",
    "(defparameter *monster-num* 12)\n",
    "(defparameter *monsters* nil)\n",
    "*monster-builders*\n",
    "*player-health*\n",
    "*player-agility*\n",
    "*player-strength*"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0c0959c1-6c21-40e1-9ada-e5a6e86f63ea",
   "metadata": {},
   "source": [
    "## Task 4. Compile code\n",
    "With the cursor in the code block above, hit Shift+Enter to execute it. When You adjusted the parameters correctly it will just prompt their names. Otherwise it will throw an error. Use `;` to comment out specific lines to search for the malicious one. The errormessage may give you insight where the malicious code is.\n",
    "\n",
    "When the parameters above are fine, compile the code below. \n",
    "\n",
    "## Task 5. Play the game\n",
    "Scroll way down beyond the orc-battle code. There is another code block that starts the battle."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fa38ee60-1efb-4448-bdb0-6b88e5efaa62",
   "metadata": {},
   "outputs": [],
   "source": [
    "(defun orc-battle ()\n",
    "  (init-monsters)\n",
    "  (init-player)\n",
    "  (game-loop)\n",
    "  (when (player-dead)\n",
    "    (princ \"You have been killed. Game Over.\"))\n",
    "  (when (monsters-dead)\n",
    "    (princ \"Congratulations! You have vanquished all of your foes.\")))\n",
    "\n",
    "(defun game-loop ()\n",
    "  (unless (or (player-dead) (monsters-dead))\n",
    "    (show-player)\n",
    "    (dotimes (k (1+ (truncate (/ (max 0 *player-agility*) 15))))\n",
    "      (unless (monsters-dead)\n",
    "        (show-monsters)\n",
    "        (player-attack)))\n",
    "    (fresh-line)\n",
    "    (map 'list\n",
    "         (lambda(m)\n",
    "           (or (monster-dead m) (monster-attack m)))\n",
    "         *monsters*)\n",
    "    (game-loop)))\n",
    "\n",
    "(defun init-player ()\n",
    "  (setf *player-health* 30)\n",
    "  (setf *player-agility* 30)\n",
    "  (setf *player-strength* 30))\n",
    "\n",
    "(defun player-dead ()\n",
    "  (<= *player-health* 0))\n",
    "\n",
    "(defun show-player ()\n",
    "  (fresh-line)\n",
    "  (princ \"You are a valiant knight with a health of \")\n",
    "  (princ *player-health*)\n",
    "  (princ \", an agility of \")\n",
    "  (princ *player-agility*)\n",
    "  (princ \", and a strength of \")\n",
    "  (princ *player-strength*))\n",
    "\n",
    "(defun player-attack ()\n",
    "  (fresh-line)\n",
    "  (princ \"Attack style: [s]tab [d]ouble swing [r]oundhouse:\")\n",
    "  (case (read)\n",
    "    (s (monster-hit (pick-monster)\n",
    "                    (+ 2 (randval (ash *player-strength* -1)))))\n",
    "    (d (let ((x (randval (truncate (/ *player-strength* 6)))))\n",
    "         (princ \"Your double swing has a strength of \")\n",
    "         (princ x)\n",
    "         (fresh-line)\n",
    "         (monster-hit (pick-monster) x)\n",
    "         (unless (monsters-dead)\n",
    "           (monster-hit (pick-monster) x))))\n",
    "    (otherwise (dotimes (x (1+ (randval (truncate (/ *player-strength* 3)))))\n",
    "                 (unless (monsters-dead)\n",
    "                   (monster-hit (random-monster) 1))))))\n",
    "\n",
    "(defun randval (n)\n",
    "  (1+ (random (max 1 n))))\n",
    "\n",
    "(defun random-monster ()\n",
    "  (let ((m (aref *monsters* (random (length *monsters*)))))\n",
    "    (if (monster-dead m)\n",
    "        (random-monster)\n",
    "      m)))\n",
    "\n",
    "(defun pick-monster ()\n",
    "  (fresh-line)\n",
    "  (princ \"Monster #:\")\n",
    "  (let ((x (read)))\n",
    "    (if (not (and (integerp x) (>= x 1) (<= x *monster-num*)))\n",
    "        (progn (princ \"That is not a valid monster number.\")\n",
    "               (pick-monster))\n",
    "      (let ((m (aref *monsters* (1- x))))\n",
    "        (if (monster-dead m)\n",
    "            (progn (princ \"That monster is alread dead.\")\n",
    "                   (pick-monster))\n",
    "          m)))))\n",
    "\n",
    "(defun init-monsters ()\n",
    "  (setf *monsters*\n",
    "        (map 'vector\n",
    "             (lambda (x)\n",
    "               (funcall (nth (random (length *monster-builders*))\n",
    "                             *monster-builders*)))\n",
    "             (make-array *monster-num*))))\n",
    "\n",
    "(defun monster-dead (m)\n",
    "  (<= (monster-health m) 0))\n",
    "\n",
    "(defun monsters-dead ()\n",
    "  (every #'monster-dead *monsters*))\n",
    "\n",
    "(defun show-monsters ()\n",
    "  (fresh-line)\n",
    "  (princ \"Your foes:\")\n",
    "  (let ((x 0))\n",
    "    (map 'list\n",
    "         (lambda (m)\n",
    "           (fresh-line)\n",
    "           (princ \"   \")\n",
    "           (princ (incf x))\n",
    "           (princ \". \")\n",
    "           (if (monster-dead m)\n",
    "               (princ \"**dead**\")\n",
    "             (progn (princ \"(Health=\")\n",
    "                    (princ (monster-health m))\n",
    "                    (princ \") \")\n",
    "                    (monster-show m))))\n",
    "         *monsters*)))\n",
    "\n",
    "(defstruct monster (health (randval 10)))\n",
    "\n",
    "(defmethod monster-hit (m x)\n",
    "  (decf (monster-health m) x)\n",
    "  (if (monster-dead m)\n",
    "      (progn (princ \"You killed the \")\n",
    "             (princ (type-of m))\n",
    "             (princ \"! \"))\n",
    "    (progn (princ \"You hit the \")\n",
    "           (princ (type-of m))\n",
    "           (princ \", knocking off \")\n",
    "           (princ x)\n",
    "           (princ \" health points! \"))))\n",
    "\n",
    "(defmethod monster-show (m)\n",
    "  (princ \"A fierce \")\n",
    "  (princ (type-of m)))\n",
    "\n",
    "(defmethod monster-attack (m))\n",
    "\n",
    "(defstruct (orc (:include monster)) (club-level (randval 8)))\n",
    "\n",
    "(push #'make-orc *monster-builders*)\n",
    "\n",
    "(defmethod monster-show ((m orc))\n",
    "  (princ \"A wicked orc with a level \")\n",
    "  (princ (orc-club-level m))\n",
    "  (princ \" club\"))\n",
    "\n",
    "(defmethod monster-attack ((m orc))\n",
    "  (let ((x (randval (orc-club-level m))))\n",
    "    (princ \"An orc swings his club at you and knocks off \")\n",
    "    (princ x)\n",
    "    (princ \" of your health points. \")\n",
    "    (decf *player-health* x)))\n",
    "\n",
    "(defstruct (hydra (:include monster)))\n",
    "\n",
    "(push #'make-hydra *monster-builders*)\n",
    "\n",
    "(defmethod monster-show ((m hydra))\n",
    "  (princ \"A malicious hydra with \")\n",
    "  (princ (monster-health m))\n",
    "  (princ \" heads.\"))\n",
    "\n",
    "(defmethod monster-hit ((m hydra) x)\n",
    "  (decf (monster-health m) x)\n",
    "  (if (monster-dead m)\n",
    "      (princ \"The corpse of the fully decapitated and decapacitated hydra falls to the floor!\")\n",
    "    (progn (princ \"You lop off \")\n",
    "           (princ x)\n",
    "           (princ \" of the hydra's heads! \"))))\n",
    "\n",
    "(defmethod monster-attack ((m hydra))\n",
    "  (let ((x (randval (ash (monster-health m) -1))))\n",
    "    (princ \"A hydra attacks you with \")\n",
    "    (princ x)\n",
    "    (princ \" of its heads! It also grows back one more head! \")\n",
    "    (incf (monster-health m))\n",
    "    (decf *player-health* x)))\n",
    "\n",
    "(defstruct (slime-mold (:include monster)) (sliminess (randval 5)))\n",
    "\n",
    "(push #'make-slime-mold *monster-builders*)\n",
    "\n",
    "(defmethod monster-show ((m slime-mold))\n",
    "  (princ \"A slime mold with a sliminess of \")\n",
    "  (princ (slime-mold-sliminess m)))\n",
    "\n",
    "(defmethod monster-attack ((m slime-mold))\n",
    "  (let ((x (randval (slime-mold-sliminess m))))\n",
    "    (princ \"A slime mold wraps around your legs and decreases your agility by \")\n",
    "    (princ x)\n",
    "    (princ \"! \")\n",
    "    (decf *player-agility* x)\n",
    "    (when (zerop (random 2))\n",
    "      (princ \"It also squirts in your face, taking away a health point! \")\n",
    "      (decf *player-health*))))\n",
    "\n",
    "(defstruct (brigand (:include monster)))\n",
    "\n",
    "(push #'make-brigand *monster-builders*)\n",
    "\n",
    "(defmethod monster-attack ((m brigand))\n",
    "  (let ((x (max *player-health* *player-agility* *player-strength*)))\n",
    "    (cond ((= x *player-health*)\n",
    "           (princ \"A brigand hits you with his slingshot, taking off 2 health points! \")\n",
    "           (decf *player-health* 2))\n",
    "          ((= x *player-agility*)\n",
    "           (princ \"A brigand catches your leg with his whip, taking off 2 agility points! \")\n",
    "           (decf *player-agility* 2))\n",
    "          ((= x *player-strength*)\n",
    "           (princ \"A brigand cuts your arm with his whip, taking off 2 strength points! \")\n",
    "           (decf *player-strength* 2)))))\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a0062239-7c6e-4982-af1e-8f91da194d52",
   "metadata": {},
   "source": [
    "## Task 5 cont'd\n",
    "The following code will initialize the orc-battle. Since this is a text adventure, every time you enter a choice, the world reacts. You'll figure it out.\n",
    "\n",
    "You have finished this assignment, when you found suitable values for games parameters, that makes it possible to win but keep it challenging. The initial number of monsters can be adjusted in the upmost code block, where the parameters are. The other parameters are set in the code.\n",
    "\n",
    "Happy hacking!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "563639a7-3238-4d0c-ac0f-9530c61bebb6",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "(orc-battle)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Common Lisp",
   "language": "common-lisp",
   "name": "common-lisp"
  },
  "language_info": {
   "codemirror_mode": "text/x-common-lisp",
   "file_extension": ".lisp",
   "mimetype": "text/x-common-lisp",
   "name": "common-lisp",
   "pygments_lexer": "common-lisp",
   "version": "2.0.1.debian"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
